/**
 * BlogPosting rendering for the Neos.Demo website
 */
prototype(Neos.Demo:Document.BlogPosting) < prototype(Neos.Demo:Document.Page) {

    structuredData.blogPosting = Neos.Seo:StructuredData.RootObject {
        type = "BlogPosting"
        attributes = Neos.Fusion:DataStructure {
            url = Neos.Neos:NodeUri {
                node = ${documentNode}
            }
            author = Neos.Seo:StructuredData.Object {
                type = "Person"
                attributes.name = ${q(node).property('authorName')}
            }
            headline = ${q(node).property('title')}
            abstract = ${q(node).property('abstract')}
            datePublished = ${Date.format(q(node).property('datePublished'), 'Y-m-d')}
            image = Neos.Seo:StructuredData.Object {
                type = "ImageObject"
                attributes.url = Neos.Neos:ImageUri {
                    asset = ${q(node).property('image')}
                    maximumWidth = 400
                    maximumHeight = 225
                    allowCropping = true
                }
            }
        }
    }

    # content is overwritten to render the intro section first
    content >
    content = Neos.Fusion:Join {

        # @todo the intro should be made pretty and extracted as presentational component
        intro = afx`
            <Neos.Neos:ImageTag asset={q(node).property('image')} maximumWidth="1000" maximumHeight="200" />
            <h1 class="text-center"><Neos.Neos:Editable property="title" block={false} /></h1>
            <p class="text-center"><Neos.Neos:Editable property="abstract" block={false} /></p>
            <p class="text-center">{Date.format(q(node).property('datePublished'), 'd.m.Y')} - {q(node).property('authorName')}</p>
        `
        intro.@process.contentElementWrapping = Neos.Neos:ContentElementWrapping

        main = Neos.Neos:PrimaryContent {
            nodePath = 'main'
            @process.convertUris = Neos.Neos:ConvertUris
        }
    }
}
