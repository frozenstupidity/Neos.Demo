##
# "BlogPostingList" element
#
prototype(Neos.Demo:Content.BlogPostingList) < prototype(Neos.Neos:ContentComponent) {

    blogArticles.@if.has = ${q(node).property('blogs')}
    blogArticles = ${q(q(node).property('blogs')).children('[instanceof Neos.Demo:Document.BlogPosting]')}
    blogArticles.@process.sortBy = ${value.sort("datePublished", "DESC")}
    blogArticles.@process.limit = ${value.slice(0, q(node).property('limit'))}

    renderer = Neos.Demo:Presentation.Cards.Container {
        content = Neos.Fusion:Loop {
            items = ${props.blogArticles}
            itemName = "blogPosting"
            itemRenderer = Neos.Demo:Presentation.Cards.Card {
                imageUri = Neos.Neos:ImageUri {
                    asset = ${q(blogPosting).property('image')}
                    maximumWidth = 400
                    maximumHeight = 225
                }
                title = ${q(blogPosting).property('title')}
                content = ${q(blogPosting).property('abstract')}
                uri = Neos.Neos:NodeUri {
                    node = ${blogPosting}
                }
            }
        }
    }

    @cache {
        mode = "cached"
        entryIdentifier {
            node = ${node}
        }
        entryTags {
            # recalculate once a blogposting was modified as this might affect the list
            1 = ${Neos.Caching.nodeTypeTag('Neos.Demo:Document.BlogPosting', node)}
        }
    }
}
